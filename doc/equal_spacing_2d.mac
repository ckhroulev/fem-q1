/* This script produces formulas for approximating line integrals over
sides of 2D Q1 elements in the regular grid case. See notes in the
latex directory. */

assume(dx > 0, dy > 0)$
texput(dx, "\\dx")$
texput(dy, "\\dy")$
texput(nounify(T), "\\frac{d}{dt}\\R")$

/* coordinates of the nodes of the reference element */
xis   : [-1,  1,  1, -1]$
etas  : [-1, -1,  1,  1]$

/* Reference element basis functions */
chi[i](xi, eta) := (1/4) * (1 + xi*xis[i]) * (1 + eta*etas[i])$

/* The isoparametric map from the reference to a physical element. */
x(xi, eta) := sum(x[j] * chi[j](xi, eta), j, 1, 4)$
y(xi, eta) := sum(y[j] * chi[j](xi, eta), j, 1, 4)$

/* sides of the reference hexahedron */
sides : [
  [-1, t], [1, t],
  [t, -1], [t, 1]
]$

/* parameterizations of sides */
R[j] := [funmake(x, sides[j]), funmake(y, sides[j])]$

equal_horizontal_spacing : [
  x[3] = x[2], x[4] = x[1], x[2] = x[1] + dx,
  y[2] = y[1], y[4] = y[3], y[3] = y[1] + dy
]$

/* tangential vector */
T[j] := diff(R[j], t)$
vnorm[j] := sqrt(T[j] . T[j])$

eq_simplify(e) := expand(subst(equal_horizontal_spacing, ev(e, nouns)))$

tex_align(makelist(abs('T[j]) = eq_simplify(vnorm[j]), j, 1, 4))$
