/* -*- mode: maxima -*- */

/* coordinates of the nodes of the reference element */
xis  : [-1,  1,  1, -1]$
etas : [-1, -1,  1,  1]$

/* Q1 shape functions */
chi[i](xi, eta) := (1/4) * (1 + xi*xis[i]) * (1 + eta*etas[i])$

/* Map from the reference element to a 2D (boundary) element
corresponding to the top (or bottom) surface. */
M(V, xi, eta) := 'sum(V[j] * 'chi[j](xi, eta), j, 1, 4)$
x(xi, eta) := M(x, xi, eta)$
y(xi, eta) := M(y, xi, eta)$
z(xi, eta) := M(z, xi, eta)$

J : matrix(
  ['diff(x(xi, eta), xi), 'diff(y(xi, eta), xi)],
  ['diff(x(xi, eta), eta), 'diff(y(xi, eta), eta)])$

/* Compute the Jacobian of the mapping from the reference element (times 4)*/
four_J : ratsimp(ev(4*J, nouns))$

/* If all elements are rectangles aligned with the coordinate system, then... */
aligned_rectangle : [
  x[4] = x[1],
  x[3] = x[2],
  x[2] = x[1] + dx,
  y[2] = y[1],
  y[4] = y[3],
  y[3] = y[1] + dy]$

J_aligned : factor(subst(aligned_rectangle, four_J) / 4);